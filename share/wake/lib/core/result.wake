# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

global data Result pass fail =
  Pass pass
  Fail fail

global def isPass = match _
  Pass _ = True
  Fail _ = False

global def isFail = match _
  Pass _ = False
  Fail _ = True

global def getPass = match _
  Pass x = Some x
  Fail _ = None

global def getFail = match _
  Pass _ = None
  Fail x = Some x

global def getWhenFail fail = match _
  Pass a = a
  Fail _ = fail

global def rmap fn = match _
  Pass a = Pass (fn a)
  Fail f = Fail f

# Applies a fallible function to Pass value or propogates Fail
# (fn: a => Result b c) => Result a c => Result b c
global def rmapPass fn = match _
  Pass a = fn a
  Fail f = Fail f

# Applies a fallible function to Fail value or propogates Pass
# (fn: a => Result b c) => Result b a => Result b c
global def rmapFail fn = match _
  Pass a = Pass a
  Fail f = fn f

# Finds the first Fail in a List of Result or aggregates Passes into a List
# List (Result a b) => Result (List a) b
global def findFail = findFailFn (_)
# Applies a fallible function to each element of a List,
#   returning the first Fail or aggregates the Passes into a List
# (fn: a => Result b c) => List a => Result (List b) c
global def findFailFn fn =
  def helper = match _
    Nil = Pass Nil
    h, t = match (fn h)
      Fail y = Fail y
      Pass x = match (helper t)
        Pass tt = Pass (x, tt)
        z = z
  helper

# Finds the first Pass in a List of Result or aggregates Fails into a List
# List (Result a b) => Result a (List b)
global def findPass = findPassFn (_)
# Applies a fallible function to each element of a List
#  returning the first Pass or aggregates the Fails into a List
# (fn: a => Result b c) => List a => Result b (List c)
global def findPassFn fn =
  def helper = match _
    Nil = Fail Nil
    h, t = match (fn h)
      Pass y = Pass y
      Fail x = match (helper t)
        Fail tt = Fail (x, tt)
        z = z
  helper

global def panic s = prim "panic"

global def stack s = prim "stack"

global tuple Error =
  global Cause: String
  global Stack: List String

global def makeError cause =
  Error cause (stack Unit)
