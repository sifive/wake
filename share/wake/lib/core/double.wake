# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package wake

# double operations

# Unary absolute value operator for a Double.
export def dabs x = prim "dabs"

# Unary negative sign for a Double.
export def   -. x = prim "dneg"

# Unary positive sign for a Double.
export def   +. x = x : Double

# Binary addition operator for Doubles.
export def x +. y = prim "dadd"

# Binary subtraction operator for Doubles.
export def x -. y = prim "dsub"

# Binary multiplication operator for Doubles.
export def x *. y = prim "dmul"

# Binary division operator for Doubles.
export def x /. y = prim "ddiv"

# Binary exponentiation operator for Doubles.
export def x ^. y = prim "dpow"

# Computes x*y + z.
export def dfma x y z = prim "dfma" # x*y + z

# Creates a function that computes the nth root.
export def droot n =
  def n1 = 1.0 /. n
  _ ^. n1

# Unary operator for square root.
export def dsqrt = droot 2.0

def root3 = droot 3.0

def root4 = droot 4.0

# Unary operator for square root, using the unicode character.
export def √ x = dsqrt x

# Unary operator for cube root.
export def ∛ x = root3 x

# Unary operator for fourth root.
export def ∜ x = root4 x

export def dcmp x y = prim "dcmp"
export def x <=>. y = prim "dcmp"

# Binary Less-Than Operator.
export def x <.  y = isLT (x <=>. y)

# Binary Greater-Than Operator.
export def x >.  y = isGT (x <=>. y)

# Binary Greater-Or-Equal Operator.
export def x >=. y = isGE (x <=>. y)

# Binary Less-Or-Equal Operator.
export def x <=. y = isLE (x <=>. y)

# Binary Equal-To Operator.
export def x ==. y = isEQ (x <=>. y)

# Binary Not-Equal Operator.
export def x !=. y = isNE (x <=>. y)

# Computes the minimum of two Doubles.
export def dmin x y = if x <. y then x else y

# Computes the maximum of two Doubles.
export def dmax x y = if x <. y then y else x

# Product of a series of Doubles.
export def dprod = foldl (_*._) 1.0
export def ∏. l = dprod l

# Sum of a series of Doubles.
export def dsum = foldl (_+._) 0.0
export def ∑. l = dsum l

export data DoubleFormat =
  DoubleFixed
  DoubleScientific
  DoubleHex
  DoubleDefault

export def dformat x =
  def imp f p x = prim "dstr"
  def f = match x
    DoubleFixed      = 0
    DoubleScientific = 1
    DoubleHex        = 2
    DoubleDefault    = 3
  imp f

export def double s =
  def p s = prim "ddbl"
  p s | head

export def dhex = dformat DoubleHex 20
export def dstr = dformat DoubleDefault 17

export def dint x = double (str x)

# Conversion methods
export data DoubleClass =
  DoubleInfinite
  DoubleNaN
  DoubleSubNormal
  DoubleNormal

export def dclass x =
  def imp x = prim "dclass"
  match (imp x)
    1 = DoubleInfinite
    2 = DoubleNaN
    4 = DoubleSubNormal
    _ = DoubleNormal

export def dfrexp x   = prim "dfrexp"
export def dldexp f e = prim "dldexp"
export def dmodf  x   = prim "dmodf"

export def idouble x = match (dmodf x)
 Pair x 0.0 = Some x
 _ = None

# Handy numerical functions

# Calculates the cosine of a Double.
export def dcos    x = prim "dcos"

# Calculates the sine of a Double.
export def dsin    x = prim "dsin"

# Calculates the tangent of a Double.
export def dtan    x = prim "dtan"

# Calculates the inverse cosine of a Double.
export def dacos   x = prim "dacos"

# Calculates the inverse sine of a Double.
export def dasin   x = prim "dasin"

# Calculates e^x.
export def dexp    x = prim "dexp"

# Calculates the natural logarithm of x.
export def dlog    x = prim "dlog"
export def dexpm1  x = prim "dexpm1"
export def dlog1p  x = prim "dlog1p"
export def derf    x = prim "derf"
export def derfc   x = prim "derfc"
export def dtgamma x = prim "dtgamma"
export def dlgamma x = prim "dlgamma"

# Calculates the inverse tangent of y/x, giving the angle of the point(x, y) in the coordinate plane. 
export def datan x y = prim "datan"

# Useful constants

# Not a Number
export def nan = 0.0 /. 0.0

# Infinity
export def inf = 1.0 /. 0.0

# Pi 
export def pi  = dacos 0.0 *. 2.0
