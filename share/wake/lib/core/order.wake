# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package wake

# Used for comparing quantities.  
# Can hold the following values:
# ``LT`` = Less Than
# ``EQ`` = Equal
# ``GT`` = Greater Than
export data Order =
    LT
    EQ
    GT

# Is less-than: convert Order to Boolean
# def a < b = a <=> b | isLT
# isLT LT = True
# isLT EQ = False
# isLT GT = False
export def isLT: Order => Boolean = match _
    LT -> True
    _ -> False

# Is equal: convert Order Boolean
# def a == b = a <=> b | isEQ
# isEQ LT = False
# isEQ EQ = True
# isEQ GT = False
export def isEQ: Order => Boolean = match _
    EQ -> True
    _ -> False

# Is greater-than: convert Order to Boolean
# def a > b = a <=> b | isGT
# isGT LT = False
# isGT EQ = False
# isGT GT = True
export def isGT: Order => Boolean = match _
    GT -> True
    _ -> False

# Is less-than-or-equal: convert Order to Boolean
# def a <= b = a <=> b | isLE
# isLE LT = True
# isLE EQ = True
# isLE GT = False
export def isLE: Order => Boolean = match _
    GT -> False
    _ -> True

# Is not-equal: convert Order to Boolean
# def a != b = a <=> b | isNE
# isEQ LT = True
# isEQ EQ = False
# isEQ GT = True
export def isNE: Order => Boolean = match _
    EQ -> False
    _ -> True

# Is greater-than-or-equal: convert Order to Boolean
# def a >= b = a <=> b | isGE
# isGE LT = False
# isGE EQ = True
# isGE GT = True
export def isGE: Order => Boolean = match _
    LT -> False
    _ -> True
