package http

from http export type HttpRequest

# The http methods that may be called against a web server.
export data HttpMethod =
    HttpMethodGet
    HttpMethodHead
    HttpMethodPost
    HttpMethodPut
    HttpMethodDelete
    HttpMethodConnect
    HttpMethodOptions
    HttpMethodTrace
    HttpMethodPatch

# The headers that may be assocated with an http request.
#
# These are often used to specify how to interpret the body or to provide authorization.
tuple HttpHeader =
    Name: String
    Value: String

data HttpFormDataValue =
    HttpFormDataString String
    HttpFormDataFile Path

tuple HttpFormData =
    Name: String
    Value: HttpFormDataValue

# The description of a http request to send to a web server.
tuple HttpRequest =
    # The url of the function to be called
    Url: String
    # The method to be called on the destination
    Method: HttpMethod
    # The headers to be set on the request
    Headers: List HttpHeader
    # A string of the contents of the request. Mutually exclusive with form data
    Body: Option String
    # Multipart form data. May be a list of strings or files. Mutually exclusive with body
    FormData: Option (List HttpFormData)

export tuple HttpResponse =
    # The status code returned from the web server. 
    #
    # Not all response have a status code so its not guarenteed to be set.
    export StatusCode: Option Integer
    # The headers returned from the web server
    export Headers: List HttpHeader
    # The body returned from the web server
    export Body: String

# Creates a basic HttpRequest to be used as part of a builder.
#
# This function should be used to start the builder chain of a desired http web request.
# Ex:
# def foo =
#     buildHttpRequest "localhost"
#     | setMethod HttpMethodGet
#     | addHeader "Content-Type" "application/json"
#     | setBody "{}"
#     | makeRequest
#
# buildHttpRequest defaults the method to GET and leave the headers and body empty.
#
# NOTE: Relying on the internet for your build is inherently unreproducible and should be avoided.
export def buildHttpRequest (url: String): HttpRequest =
    HttpRequest url HttpMethodGet Nil None None

# Sets an http method to the http request.
export def setMethod (method: HttpMethod): HttpRequest => HttpRequest =
    setHttpRequestMethod method

# Adds an arbitary header to the http request.
#
# This function is normally used for custom or uncommon headers. Common headers should use
# a dedicated function.
export def addHeader (name: String) (value: String): HttpRequest => HttpRequest =
    editHttpRequestHeaders ((HttpHeader name value), _)

# Adds the Content-Type header to the http request.
export def addContentTypeHeader (value: String): HttpRequest => HttpRequest =
    addHeader "Content-Type" value

# Adds the Content-Type header to the http request and sets it to application/json
export def addContentTypeJsonHeader: HttpRequest => HttpRequest =
    addContentTypeHeader "application/json"

# Adds the Authorization header to the http request.
export def addAuthorizationHeader (value: String): HttpRequest => HttpRequest =
    addHeader "Authorization" value

# Sets the body to the http request.
export def setBody (body: String): HttpRequest => HttpRequest =
    setHttpRequestBody (Some body)

# Adds a named string value to the requests form data
export def addFormDataString (name: String) (value: String): HttpRequest => HttpRequest =
    def setOrAppend field =
        def entry = HttpFormData name (HttpFormDataString value)

        match field
            Some formDatas -> Some (entry, formDatas)
            None -> Some (entry, Nil)

    editHttpRequestFormData setOrAppend

# Adds a named file to the requests form data
export def addFormDataFile (name: String) (value: Path): HttpRequest => HttpRequest =
    def setOrAppend field =
        def entry = HttpFormData name (HttpFormDataFile value)

        match field
            Some formDatas -> Some (entry, formDatas)
            None -> Some (entry, Nil)

    editHttpRequestFormData setOrAppend

# Helper tuple for parsing a curl response
tuple CurlResponseParts =
    StatusCode: String
    Headers: List String
    Body: String

# Helper for chunking a curl response stream into parts
def parseCurlResponse (str: String): Result HttpResponse Error =
    # Response format:
    #
    # HTTP/1.1 415 Unsupported Media Type
    # content-length: 54
    # other: header
    # other: header
    #
    # Expected request with `Content-Type: application/json`
    require Pass (CurlResponseParts codeline headers body) =
        def parts = tokenize `\r?\n` str

        require code, rest = parts
        else failWithError "Unexpected empty curl response"

        # each header is on its own line, headers stop when we reach an empty line
        def headers = takeUntil (_ ==* "") rest

        # The body is just the remaining lines minus the header lines and empty line
        def body =
            rest
            | drop (headers.len + 1)
            | catWith "\n"

        Pass (CurlResponseParts code headers body)

    require Pass status =
        require (code, Nil) = extract `^HTTP\/[\d\.]+ (\d+) .*$` codeline
        else Pass None

        require (Some x) =
            code
            | (intbase 10)
        else failWithError "unable to parse {code} as a base 10 integer"

        Pass (Some x)

    require Pass headers =
        def loop header =
            require (name, value, Nil) = extract `^([-_A-Za-z0-9]+):\s*(.+)$` header
            else failWithError "Unable to extract header from '{header}'"

            Pass (HttpHeader name value)

        headers
        | map loop
        | findFail

    Pass (HttpResponse status headers body)

# Makes the request specified by request and parses into a HttpResponse
#
# WARNING: This function will likely break the sandbox and make your build unreliable
export def makeRequest ((HttpRequest url method headers body formData): HttpRequest): Result HttpResponse Error =
    def methodToString = match _
        HttpMethodGet -> "GET"
        HttpMethodHead -> "HEAD"
        HttpMethodPost -> "POST"
        HttpMethodPut -> "PUT"
        HttpMethodDelete -> "DELETE"
        HttpMethodConnect -> "CONNECT"
        HttpMethodOptions -> "OPTIONS"
        HttpMethodTrace -> "TRACE"
        HttpMethodPatch -> "PATCH"

    def headerToCurlFlag (HttpHeader name value) = "--header", "{name}:{value}", Nil
    def bodyToCurlFlag body = "--data", body, Nil

    def formDataToCurlFlag (HttpFormData name value) = match value
        HttpFormDataString str -> "--form", "{name}={str}", Nil
        HttpFormDataFile file -> "--form", "{name}=@{file.getPathName}", Nil

    require False = body.isSome && formData.isSome
    else failWithError "Request Body and FormData are both set. This is not allowed."

    def cmd =
        (
            Some ("curl", Nil),
            Some ("-i", Nil), # Include response headers
            Some ("--request", method | methodToString, Nil),
            Some ("--url", url, Nil),
            Some (headers | mapFlat headerToCurlFlag),
            (body | omap bodyToCurlFlag),
            (formData | omap (mapFlat formDataToCurlFlag)),
        )
        | mapPartial (_)
        | flatten

    require Pass stdout =
        makeExecPlan cmd Nil
        | setPlanLabel "http: {method | methodToString} {url}"
        | setPlanStdout logInfo
        # Curl dumps download stats to stderr
        | setPlanStderr logInfo
        # Web is unreliable and should never be reused
        | setPlanPersistence ReRun
        | runJobWith localRunner
        | getJobStdout

    parseCurlResponse stdout
