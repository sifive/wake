export def datFile Unit =
    write "datFile" "hello world"

export def goodLink Unit =
    require Pass dat =
        datFile Unit
    makeExecPlan ("ln", "-s", "datFile", "goodLink", Nil) (dat, Nil)
    | setPlanLabel "test (symlinks): goodLink"
    | setPlanKeep False
    | runJob
    | getJobOutput

export def badLink Unit =
    makeExecPlan ("ln", "-s", "badFile", "badLink", Nil) Nil
    | setPlanLabel "test (symlinks): badLink"
    | setPlanKeep False
    | runJob
    | getJobOutput

export def shouldWork Unit =
    require Pass link =
        goodLink Unit
    require Pass dat =
        datFile Unit
    makeExecPlan ("cat", "goodLink", Nil) (link, dat, Nil)
    | setPlanLabel "test (symlinks): cat goodLink"
    | editPlanEnvironment (setEnvironment "TEST" "1")
    | setPlanKeep False
    | runJob
    | getJobInputs

export def shouldFail1 Unit =
    require Pass link =
        goodLink Unit
    makeExecPlan ("cat", "goodLink", Nil) (link, Nil)
    | setPlanLabel "test (symlinks): cat goodLink shouldFail"
    | editPlanEnvironment (setEnvironment "TEST" "2")
    | setPlanKeep False
    | runJob
    | getJobInputs

export def shouldFail2 Unit =
    require Pass dat =
        datFile Unit
    makeExecPlan ("cat", "goodLink", Nil) (dat, Nil)
    | setPlanLabel "test (symlinks): cat goodLink shouldFail2"
    | editPlanEnvironment (setEnvironment "TEST" "3")
    | setPlanKeep False
    | runJob
    | getJobInputs

export def test _ =
    require Pass link = badLink Unit
    require True = getPathHash link !=* "BadHash"
    else failWithError "cannot hash a dangling symlink"
    require Pass what = shouldWork Unit
    require Fail _ = shouldFail1 Unit else failWithError "fail1 passed!"
    require Fail _ = shouldFail2 Unit else failWithError "fail2 passed!"
    require a, b, Nil = what
    else failWithError "did not detect both inputs ({format what})"
    require True = getPathHash a !=* getPathHash b
    else failWithError "identical hashes should not be identical"
    Pass 0
