from wake import _

publish source =
    "some-input.txt",

def mkTestPlan (s: String): Result Plan Error =
    require Pass source = source "some-input.txt"

    makePlan
    "runner_hash_test"
    (source,)
    "cat some-input.txt > test.txt && cat some-input.txt >> test.txt"
    | setPlanResources (s,)
    | Pass

def resourceAwareCacheRunner =
    def hashResource x =
        require Pass input = x

        Pass (catWith "." input.getRunnerInputResources)

    mkJobCacheRunner hashResource "/workspace" fuseRunner

def runTestJob (s: String) =
    require Pass plan = mkTestPlan s

    require Pass outputs =
        plan
        | runJobWith resourceAwareCacheRunner
        | getJobOutputs

    Pass (map (_.getPathName) outputs)

export def test1 (_: List String): Result (List String) Error =
    runTestJob "test1"

export def test2 (_: List String): Result (List String) Error =
    runTestJob "test2"

