# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package build_wake
from wake import _
from gcc_wake import _

def version_h Unit =
    match (buildAs Unit)
        Fail f = makeBadPath f
        Pass ver = write "{here}/version.h" "#define VERSION {ver}\n"

def pkg name = match (pkgConfig name)
    Some x = x
    None   = makeSysLib "" | editSysLibLFlags ("-l{name}", _)

def ncurses Unit =
    pkgConfig "ncurses tinfo" | getOrElseFn (\Unit pkg "ncurses")

def buildWake (Pair variant clib) =
    def internalDeps = common variant, map (_ clib) (utf8proc, gopt, Nil)
    def externalDeps = ncurses Unit, map pkg ("sqlite3", "gmp", "re2", Nil)
    def deps = internalDeps ++ externalDeps | flattenSysLibs
    def reFiles = sources here `.*\.re`
    def headerFiles = version_h Unit, deps.getSysLibHeaders ++ sources here `.*\.h`
    def cppFiles = map re2c reFiles ++ sources here `.*\.cpp`
    def compile = compileC variant deps.getSysLibCFlags headerFiles
    def objFiles = map compile cppFiles ++ deps.getSysLibObjects
    linkO variant deps.getSysLibLFlags objFiles "bin/wake"
