# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package build_wake
from wake import _

def buildAs Unit = match Nil #(subscribe releaseAs)
  version, Nil = Pass version
  _ =
    def cmdline = which "git", "describe", "--tags", "--dirty", Nil
    makePlan cmdline Nil
    | setPlanKeep False
    | setPlanLocalOnly True
    | setPlanStdout logNever
    | setPlanEcho Verbose
    | runJob
    | getJobStdout
    | rmap (replace `^v|\n.*` '')
    | rmap (replace `-([0-9]{1,3})-` '.\1+')
    | rmap (replace `-` '~')
    # The replace accomplish this:
    # v0.15.0-alpha-4-g6efe8e9-dirty ... for a tag v0.15.0-alpha with +4 commits
    #  0.15.0~alpha.4+g6efe8e9~dirty ... which sorts correctly for both rpm+deb
    # ^      ^     ^ ^        ^

def buildOn Unit = match Nil #(subscribe releaseOn)
  date, Nil = Pass date
  _ =
    def cmdline = which "git", "show", "-s", "--format=%ci", "HEAD", Nil
    makePlan cmdline Nil
    | setPlanKeep False
    | setPlanLocalOnly True
    | setPlanStdout logNever
    | setPlanEcho Verbose
    | runJob
    | getJobStdout
    | rmap (replace `\n.*` '')
