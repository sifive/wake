# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package build_wake
from wake import _
from gcc_wake import _

def runtime (variant: String): Result SysLib Error =
    def externalDeps =
        ncurses Unit, map pkg ("sqlite3", "gmp", "re2", Nil)

    def internalDepResults =
        common variant, map (_ "native-c99-release") (utf8proc, Nil)

    require Pass internalDeps =
        findFail internalDepResults

    def deps =
        internalDeps ++ externalDeps
        | flattenSysLibs

    def cflags =
        "-I{here}", deps.getSysLibCFlags

    def headers =
        sources here `[^/]*\.h` ++
        sources here `frontend/.*\.h` ++
        sources here `optimizer/.*\.h` ++
        sources here `runtime/.*\.h` ++
        sources here `types/.*\.h` ++
        deps.getSysLibHeaders
        
    def compile =
        compileC variant cflags headers

    def re2obj re =
        require Pass cpp = re2c re
        compile cpp

    def cppSources =
        sources here `frontend/.*\.cpp` ++
        sources here `optimizer/.*\.cpp` ++
        sources here `runtime/.*\.cpp` ++
        sources here `types/.*\.cpp`

    def objResults =
        re2obj (source "{here}/frontend/symbol.re"),
        map compile cppSources

    require Pass foundObjects =
        findFail objResults

    def objects =
        foundObjects.flatten ++
        deps.getSysLibObjects

    Pass (SysLib "0.0" headers objects cflags deps.getSysLibLFlags)
