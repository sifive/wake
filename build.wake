# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package build_wake
from wake import _

# Useful build variants (arguments to all)
def default = Pair "native-cpp11-release" "native-c99-release"
def debug   = Pair "native-cpp11-debug" "native-c99-debug"

export def build = match _
  "default", Nil = all default
  "debug",   Nil = all debug
  "tarball", Nil = tarball Unit
  _ = Fail "no target specified (try: build default/debug/tarball)".makeError

export def install = match _
  dest, Nil = doInstall (in cwd dest)
  _ = Fail "no directory specified (try: install /opt/local)".makeError

# Build all wake targets
def targets = buildWake, buildFuse, buildFuseDaemon, buildShim, buildPreload, buildPrelib, Nil
def allOut variant = map (_ variant) targets
def all variant =
  allOut variant
  | findSomeFn getPathError
  | getOrPass "BUILT"

# Install wake into a target location
def doInstall dest =
  def datfiles = sources "{here}/share" `.*`
  def binfiles = allOut default
  def releaseBin exe = installAs "{dest}/{replace `\.[^.]*$` '' exe.getPathName}" exe
  def datinstall = map (installIn dest) datfiles
  def bininstall = map releaseBin binfiles
  def readme = installIn "{dest}/share/doc/wake" (source "README.md")
  def install = readme, bininstall ++ datinstall
  install
  | findSomeFn getPathError
  | getOrPass "INSTALLED"

# Create a release tarball
def tarball Unit = match (buildAs Unit) (buildOn Unit)
  (Fail f) _ = Fail f
  _ (Fail f) = Fail f
  (Pass release) (Pass time) =
    # Create debian + RedHat package files
    def pkgfiles =
      def setVersion file =
        def in = source "{file}.in"
        def script = "%
          set -e
          sed "s/@VERSION@/%{release}/g" "%{in.getPathName}" > "%{file}.tmp"
          mv "%{file}.tmp" "%{file}"
          %"
        shellJob script (in, Nil) | getJobOutput
      map setVersion ("debian/changelog", "wake.spec", Nil)
    # Identify those sources files to include in the tarball
    def srcs =
      sources "." `([^.][^/]*/)*[^.][^/]*` # ignore .foo/ files srcs
      | filter (! matches `(debian/|wake.spec).*` _.getPathName) # omit packaging files
    def pub =
      srcs
      | map (_.getPathName.format)
      | foldr (_, ",\n  ", _) Nil
      | cat
    def manifestStr = "%
      # Generated by 'wake tarball Unit':
      package build_wake
      from wake import source , Nil
      
      publish releaseAs = '%{release}', Nil
      publish releaseOn = '%{time}', Nil
      publish source =
        %{pub}Nil
      %"
    # Create a manifest which describes the release and source files
    def manifest = write "manifest.wake" manifestStr
    # Execute tar to create a tarball of manifest + sources
    def tarball =
      def cmd =
        def gnutar = which "gnutar"
        def tar = if gnutar ==* "gnutar" then which "tar" else gnutar
        def files = map getPathName (manifest, srcs) | sortBy (_<*_)
        tar, "--mtime={time}", "--transform=s@^@wake-{release}/@",
        "--owner=0", "--group=0", "--numeric-owner", "-cJf",
        "wake_{release}.tar.xz", files
      job cmd (manifest, srcs)
      | getJobOutput
    (tarball, pkgfiles)
    | findSomeFn getPathError
    | getOrPass "TARBALL"
