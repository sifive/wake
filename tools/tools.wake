# Copyright 2019 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package build_wake

from wake import _
from gcc_wake import _

def tool (dir: String) (variant: Pair String String) (output: String) (deps: List (Pair String String => Result SysLib Error)): Result (List Path) Error =
    def Pair cppVariant cVariant =
        variant

    require Pass libs =
        map (_ variant) deps
        | findFail

    def lib =
        flattenSysLibs libs

    def headers =
        lib.getSysLibHeaders ++
        sources dir `.*\.h`

    def cflags =
        lib.getSysLibCFlags

    def objectResults =
        map (compileC cVariant   cflags headers) (sources dir `.*\.c`) ++
        map (compileC cppVariant cflags headers) (sources dir `.*\.cpp`)

    require Pass objects =
        findFail objectResults

    def allObjects =
        lib.getSysLibObjects ++
        objects.flatten

    def linkKind =
        if empty (sources dir `.*\.cpp`) then cVariant else cppVariant

    linkO linkKind lib.getSysLibLFlags allObjects output
