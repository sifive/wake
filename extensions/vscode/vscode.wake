# Copyright 2021 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package build_wake

from wake import _

def installRO path =
    def dest = "{@here}/{path.getPathName}"

    require Pass destDir =
        simplify "{dest}/.."
        | mkdir

    def inputs = destDir, path, Nil

    "install -m 444 {path.getPathName} {dest}"
    | makePlan "install: {dest}" inputs
    | runJobWith defaultRunner
    | getJobOutput

def createStdlibJSON (stdlibSources: List Path): Result Path Error =
    def formatPath path =
        getPathName path
        | replace `share/wake/lib` ""

    def formattedPaths = map formatPath stdlibSources
    def content = JObject ("files" :-> JArray (map JString formattedPaths), Nil)

    write "{@here}/stdlib.json" (prettyJSON content)

export def vscode _: Result Path Error =
    require Pass variant = toVariant "wasm"
    require Pass files = buildLSP variant

    require js, Nil = filter (matches `.*\.wasm-.*` _.getPathName) files
    else failWithError "Did not find empscripten javascript output; got: {format files}"

    require Pass jsFile = installAs "{@here}/lsp-server/wasm/lsp-wake.js" js
    require Pass packageLockJSON = source "{@here}/package-lock.json"
    require Pass release = buildAs Unit
    require Pass packageJSON = setVersion (replace `~` "-" release) "{@here}/package.json"

    require Pass npm =
        whichInEnvPath "npm"
        | getOrFail "Unable to find npm in $PATH".makeError

    def emsdkBinDir = dirname npm

    require Pass nodeModules =
        makeExecPlan (npm, "ci", Nil) (packageJSON, packageLockJSON, Nil)
        | setPlanLabel "npm ci: Install npm dependencies"
        | editPlanEnvironment (addEnvironmentPath emsdkBinDir)
        | editPlanEnvironment ("EMCC_DEBUG=1", _)
        | setPlanDirectory @here
        # NPM installs its dependencies from the web, and so can't be fully sandboxed.
        | setPlanShare False
        | runJobWith defaultRunner
        | getJobOutputs

    require Pass stdlibSources = sources "share/wake/lib" `.*`

    require Pass stdlib =
        stdlibSources
        | findFailFn installRO

    require Pass stdlibJSON = createStdlibJSON stdlibSources
    require Pass currentDirSources = sources @here `[^/]*`
    require Pass imagesSources = sources @here `images/.*`
    require Pass clientSources = sources @here `lsp-client/.*`
    require Pass serverSources = sources @here `lsp-server/.*`
    require Pass syntaxSources = sources @here `syntaxes/.*`
    require Pass vsceStdin = source "{@here}/vsce-stdin"

    def vsceFiles =
        vsceStdin,
        jsFile,
        packageJSON,
        stdlibJSON,
        currentDirSources ++
        imagesSources ++
        clientSources ++
        serverSources ++
        syntaxSources ++
        stdlib ++
        nodeModules

    require Pass npx =
        whichInEnvPath "npx"
        | getOrFail "Unable to find npx in $PATH".makeError

    require Pass outputs =
        makeExecPlan (npx, "@vscode/vsce@2.15.0", "package", Nil) vsceFiles
        | setPlanLabel "vsce: Package vsix extension"
        | editPlanEnvironment (addEnvironmentPath emsdkBinDir)
        | setPlanDirectory @here
        | setPlanStdin (relative @here vsceStdin.getPathName)
        | setPlanIsAtty True
        | runJobWith defaultRunner
        | getJobOutputs

    require vsix, Nil = filter (matches `.*\.vsix` _.getPathName) outputs
    else failWithError "vsce failed to produce vsix output; got: {format files}"

    Pass vsix
